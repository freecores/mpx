GROUP("libgcc.a"
	  "libstd.a")

MEMORY
{
  sram (rwx) : ORIGIN = 0x00002000, LENGTH = 16K
}

SECTIONS
{	
	/* first section is .text which is used for code */
	.text :
	{
		*(.text .text.*)           /* remaining code */
		*(.rodata)                 /* read-only data (constants) */
		*(.rodata*)
		*(.rdata*)
		. = ALIGN(4);
	} > sram

	/* .data section which is used for initialized data */
	.data : 
	{
    *(.got.plt) *(.got)
    *(.shdata)
    *(.data .data.* .gnu.linkonce.d.*)
    . = ALIGN(16);
    _gp = . + 0x7FF0;
    *(.lit8)
    *(.lit4)
    *(.sdata .sdata.* .gnu.linkonce.s.*)
    . = ALIGN (8);
    *(.ram)
    . = ALIGN (8);
    _edata = .;
	} > sram	
   
  .sbss : 
  {
	. = ALIGN(4);
	_sbss_start = . ;
	
    *(.sbss*)
    *(.scommon*)
  } > sram
  
  .bss : 
  {
	. = ALIGN(4);
    _bss_start = . ;
    
    *(.bss*)
    *(COMMON)
    /* Allocate room for stack */
    .   =  ALIGN(8) ;
    .   += 256 ;
    _sp =  . - 16;
  } > sram

  . = ALIGN(4);
  _end = . ;
}
